apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'maven-publish'

sourceCompatibility = 1.5
mainClassName = "com.badoo.hprof.cruncher.HprofCruncher"
group = 'com.badoo.hprof.cruncher'
version = '1.1'

repositories {
    maven { url "https://repo1.maven.org/maven2/" }
}

run {
    if(project.hasProperty('args')){
        args project.args.split('\\s+')
    }
}

task jarWithDependencies(type: Jar) {
    manifest {
        attributes 'Main-Class': 'com.badoo.hprof.cruncher.HprofCruncher'
    }
    baseName = project.name + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

dependencies {
    compile project(':hprof-lib')
    compile project(':bmd-lib')
    testCompile 'org.apache.commons:commons-io:1.3.2'
    testCompile group: 'junit', name: 'junit', version: '4.11'
}
